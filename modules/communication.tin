#ACTION {^silent_paste_END_OF_PASTE_TEXT$}
{
	#replace {pasteText} {silent_%*_} {};
	#replace {pasteText} {_casting_%*   } {};
	#replace {pasteText} { %s} { };
	#format {pasteText} {%.500s} {$pasteText};
	$pasteChannel $pasteText
}
{2}

#ACTION {^%S has restored you to full health!$}
{
	Add_Event %0;
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{5}

#ACTION {^You %w: %3}
{
	#if {"%2" != "friend" && "$channelSounds" == "true"}
	{
		#switch {"%2"}
		{
			#case {"bovine"}
			{
				synth_play bovineChannelSound
			};
			#case {"kelnale"}
			{
				synth_play kelnaleChannelSound
			};
			#case {"xp"}
			{
				synth_play xpChannelSound
			};
			#case {"zt"}
			{
				seq_play channels/zt
			};
			#default
			{
				synth_play broadcastSound
			}
		}
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{5}

#ACTION {^%S cow{say|says}, '749d7217f438bcbe1559047c940b7010'$}
{
	#showme {Go %1!};
	#line {gag};
	#if {"$channelSounds" == "true"}
	{
		seq_play i-scored
	}
}
{7}

#ACTION {%1-EBG31@ %2}
{
	#line {gag};
	#script {unencryptedMessage} {echo "%2" | rev | scripts/msgcrypt.sh | $sed "s/^'//"};
	#variable {unencryptedMessage} {${unencryptedMessage[1]}};
	#echo {%1${unencryptedMessage}'};
	#if {"$channelSounds" == "true"}
	{
		play cryptmsg
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %1${unencryptedMessage}'
	}
}
{8}

#ACTION {^You %1 '%2'.$}
{
	#if {"%1" != "{ct|yell}" && "$channelSounds" == "true"}
	{
		#switch {"%1"}
		{
			#case {"auction"}
			{
				synth_play auctionSendSound
			};
			#case {"chat"}
			{
				synth_play chatSendSound
			};
			#case {"cowsay"}
			{
				synth_play cowsaySendSound
			};
			#case {"gossip"}
			{
				synth_play gossipSendSound
			};
			#case {"music"}
			{
				synth_play musicSendSound
			};
			#case {"say"}
			{
				synth_play saySendSound
			};
			#case {"{tell|reply to} %w"}
			{
				synth_play tellSendSound
			};
			#case {"tell the clan"}
			{
				#if {"$cspam" == "true"}
				{
					#showme {You ct, '%2'};
					#line {gag}
				};
				synth_play clantellSendSound;
				#break
			};
			#case {"tell the group"}
			{
				#if {"$cspam" == "true"}
				{
					#line {ignore} {#showme {You gt, '%2'}};
					#line {gag}
				};
				synth_play grouptellSendSound
			};
			#case {"warn the group"}
			{
				synth_play groupWarnSound
			};
			#default
			{
				synth_play defaultSendSound
			}
		}
	}
}
{8}

#ACTION {^%S %2 '%3'.$}
{
	#if {"%1" != "{A|An|The|You}" && "%2" != "yell" && "%2" != "{.*}utters the word" && "$channelSounds" == "true"}
	{
		#if {"${speech.enabled}" == "true"}
		{
			speak %0
		};
		#switch {"%2"}
		{
			#case {"auctions"}
			{
				synth_play auctionReceiveSound
			};
			#case {"chats"}
			{
				synth_play chatReceiveSound
			};
			#case {"cowsays"}
			{
				synth_play cowsayReceiveSound
			};
			#case {"gossips"}
			{
				synth_play gossipReceiveSound
			};
			#case {"musics"}
			{
				synth_play musicReceiveSound
			};
			#case {"says"}
			{
				synth_play sayReceiveSound
			};
			#case {"{tells you|replies}"}
			{
				synth_play tellReceiveSound
			};
			#case {"tells the clan"}
			{
				#if {"$cspam" == "true"}
				{
					#showme {%1 ct, '%3'};
					#line {gag}
				};
				synth_play clantellReceiveSound;
				#break
			};
			#case {"tells the group"}
			{
				#if {"$cspam" == "true"}
				{
					#line {ignore} {#showme {%1 gt, '%3'}};
					#line {gag}
				};
				synth_play grouptellReceiveSound
			};
			#case {"warns the group"}
			{
				synth_play groupWarnSound
			};
			#default
			{
				#if {"%2" == "{rep|tel}{.*}" && "$awayMessage" != "false"}
				{
					reply $awayMessage
				};
				synth_play defaultReceiveSound
			}
		}
	}
}
{9}

#ACTION {^%S has given you a gift!$}
{
	#if {"$autothank" == "true"}
	{
		#nop {Randomly generate thank you message.};
		#math {i} {1d4};
		#switch {$i}
		{
			#case {1}
			{
				#variable {msg} {Awesome, thanks.}
			};
			#case {2}
			{
				#variable {msg} {Awesome, thank you.}
			};
			#case {3}
			{
				#variable {msg} {Thanks.}
			};
			#default
			{
				#variable {msg} {Thank you.}
			}
		};
		#math {i} {4d2+1};
		#delay {0.$i} {tell %1 $msg}
	};
	Add_Event %0;
	#if {"${speech.enabled}" == "true"}
	{
		#showme {speak %0}
	}
}
{9}

#ACTION {^%w {imm|imms}: %*}
{
	#if {"$channelSounds" == "true"}
	{
		#if {"%1" == "You"}
		{
			synth_play immSendSound
		};
		#else
		{
			synth_play immReceiveSound
		}
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{9}

#ACTION {^(Log in tomorrow for another december advent calendar gift!)$}
{
	gift | tail -n 1 | prefix silent_
}
{9}

#ACTION {^(friend) %1 has entered the game.$}
{
	#if {"$channelSounds" == "true"}
	{
		#variable {friendNameEnter} {%1};
		play friend-enter
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{9}

#ACTION {^(friend) %1 has left the game.$}
{
	#if {"%1" == "$friendNameEnter"}
	{
		#unvariable {friendNameEnter}
	};
	#if {"$channelSounds" == "true"}
	{
		play friend-leave
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{9}

#ACTION {^(friend) %S was kille%*}
{
	#if {"$channelSounds" == "true"}
	{
		play friend-death
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{9}

#ACTION {^Clan channel is now {off|on}.$}
{
	synth_play notificationSound
}
{9}

#ACTION {^THE SERVER HAS DETECTED FILESYSTEM CORRUPTION.}
{
	synth_play attention
}
{9}

#ACTION {^Turning {off|on} channel '%*'.}
{
	synth_play notificationSound
}
{9}

#ACTION {^Welcome back! Thanks for playing Alter Aeon!$}
{
	gift | tail -n 1 | prefix silent_;
	synth_play dailyGift
}
{9}

#ACTION {^You can now learn the {skill|spell} '%2'!$}
{
	Add_Event %0
}
{9}

#ACTION {^You gain favor in the eyes of %S!$}
{
	Add_Event %0;
	play favor;
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{9}

#ACTION {^silent_Your gift was:%1}
{
	Add_Event Your gift was:%1
}
{9}

#ACTION {^silent_paste_%*}
{
	#variable {pasteText} {$pasteText %1}
}
{9}

#ACTION {^{>|> }***END_REPLAY****<$}
{
	#variable {channelSounds} {true};
	#line {gag}
}
{9}

#ACTION {^{\(|\[}%S{\)|\]} %S: %5}
{
	#if {"%2" != "friend" && "$channelSounds" == "true"}
	{
		#switch {"%2"}
		{
			#case {"bovine"}
			{
				synth_play bovineChannelSound
			};
			#case {"kelnale"}
			{
				synth_play kelnaleChannelSound
			};
			#case {"xp"}
			{
				synth_play xpChannelSound
			};
			#default
			{
				synth_play broadcastSound
			}
		}
	};
	#if {"${speech.enabled}" == "true"}
	{
		speak %0
	}
}
{9}

#ALIAS {display_channel %1 %2 %3}
{
	#if {%2 <= 0}
	{
		replay %3 | head -2
	};
	#elseif {%2 > 0 && %2 < 5 || %2 == 8 || %2 == 9}
	{
		replay %1 %3 | head -2
	};
	#elseif {%2 == 5}
	{
		#showme {${eventList[%3]}}
	};
	#elseif {%2 == 6}
	{
		replay | grep -i (friend) | tail %3 | head -1
	};
	#else
	{
		replay | grep -i the group, ' | tail %3 | head -1
	};
	#variable {replaying} {true};
	#delay {2} {#unvariable {replaying}}
}
{5}

#ALIAS {^crypttell %s%S %s%4}
{
	#variable {encryptedMessage} {%4};
	#replace {encryptedMessage} {"} {\"};
	#script {encryptedMessage} {echo "$encryptedMessage" | rev | scripts/msgcrypt.sh};
	#variable {encryptedMessage} {${encryptedMessage[1]}};
	tell %2 -EBG31@  $encryptedMessage
}
{8}

#ALIAS {^pimptell %S %2}
{
	#script {echo -n "tell %1 "; curl -s --data-urlencode English="%2" --data-urlencode submit="Talk Like a Pimp " http://joel.net/EBONICS/translator/ | grep -A9 '<div class="bubble1 comicfont">' | $sed -e '/<div/d' -e '/<\/div>/d' -e "s/&#39;/'/g" | tr "\n" " "}
}
{8}

#ALIAS {^Add_Event %1}
{
	#list {eventList} {insert} {1} {%1};
	#list {eventList} {size} {i};
	#if {$i >= 11}
	{
		#list {eventList} {delete} {11}
	};
	#unvariable {i}
}
{9}

#ALIAS {^away %1}
{
	#variable {awayMessage} {%1};
	#showme {Your away message is set to: $awayMessage}
}
{9}

#ALIAS {^away$}
{
	#variable {awayMessage} {false};
	#showme {away message cleared.}
}
{9}

#ALIAS {^crypt%S %2}
{
	#variable {encryptedMessage} {%2};
	#replace {encryptedMessage} {"} {\"};
	#script {encryptedMessage} {echo "$encryptedMessage" | rev | scripts/msgcrypt.sh};
	#variable {encryptedMessage} {${encryptedMessage[1]}};
	%1 -EBG31@  $encryptedMessage
}
{9}

#ALIAS {^fp %1}
{
	friend post %1
}
{9}

#ALIAS {^is{c|co|cor|core|cored}$}
{
	cowsay 749d7217f438bcbe1559047c940b7010
}
{9}

#ALIAS {^paste %1>%2}
{
	#variable {pasteText} {};
	#variable {pasteChannel} {%2};
	%1 | prefix silent_paste_;
	lecho silent_paste_END_OF_PASTE_TEXT
}
{9}

#ALIAS {^pig%S %s%3}
{
	#script {echo -n "%1 ";echo "%3" | pig}
}
{9}

#ALIAS {^pimp%+ %2}
{
	#script {echo -n "%1"; curl -s --data-urlencode English="%2" --data-urlencode submit="Talk Like a Pimp " http://joel.net/EBONICS/translator/ | grep -A9 '<div class="bubble1 comicfont">' | $sed -e '/<div/d' -e '/<\/div>/d' -e "s/&#39;/'/g" | tr "\n" " "}
}
{9}

#ALIAS {^repla%*}
{
	#variable {channelSounds} {false};
	#send {%0};
	lecho >***END_REPLAY****<
}
{9}

#GAG              {^{Clantell| Clantell} replay$}
#GAG              {^{Recent| Recent} channel history$}
#GAG              {^{Recent| Recent} tells:$}
#GAG              {^{Sending| Sending} activity for channel '%*}
#MACRO {\e!}
{
	#variable {channelBufferIndex} {0};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e#}
{
	#variable {channelBufferIndex} {2};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e$}
{
	#variable {channelBufferIndex} {3};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e%}
{
	#variable {channelBufferIndex} {4};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e&}
{
	#variable {channelBufferIndex} {6};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e(}
{
	#variable {channelBufferIndex} {8};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e)}
{
	#variable {channelBufferIndex} {9};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e*}
{
	#variable {channelBufferIndex} {7};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e-}
{
	#if {!&{channelBufferIndex} || $channelBufferIndex <= 0}
	{
		#variable {channelBufferIndex} {0}
	};
	#else
	{
		#math {channelBufferIndex} {$channelBufferIndex - 1}
	};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e0}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 10
}

#MACRO {\e1}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 1
}

#MACRO {\e2}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 2
}

#MACRO {\e3}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 3
}

#MACRO {\e4}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 4
}

#MACRO {\e5}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 5
}

#MACRO {\e6}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 6
}

#MACRO {\e7}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 7
}

#MACRO {\e8}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 8
}

#MACRO {\e9}
{
	display_channel $channelBuffer[$channelBufferIndex] $channelBufferIndex 9
}

#MACRO {\e=}
{
	#if {!&{channelBufferIndex} || $channelBufferIndex >= $totalChannels}
	{
		#variable {channelBufferIndex} {$totalChannels}
	};
	#else
	{
		#math {channelBufferIndex} {$channelBufferIndex + 1}
	};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e@}
{
	#variable {channelBufferIndex} {1};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e[1;3C}
{
	#if {!&{channelBufferIndex} || $channelBufferIndex >= $totalChannels}
	{
		#variable {channelBufferIndex} {$totalChannels}
	};
	#else
	{
		#math {channelBufferIndex} {$channelBufferIndex + 1}
	};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#MACRO {\e^}
{
	#variable {channelBufferIndex} {5};
	#showme {Channel buffer set to $channelBuffer[$channelBufferIndex]}
}

#VARIABLE         {awayMessage}  {false}
#VARIABLE         {channelBuffer}  {{0}{all}{1}{auction}{2}{bovine}{3}{cowsay}{4}{ct}{5}{events}{6}{friends}{7}{grouptells}{8}{music}{9}{tells}}
#VARIABLE         {channelSounds}  {true}
#VARIABLE         {encryptedMessage}  {}
#VARIABLE         {eventList}  {}
#VARIABLE         {pasteChannel}  {}
#VARIABLE         {pasteText}  {}
#VARIABLE         {totalChannels}  {9}
#VARIABLE         {unencryptedMessage}  {}
